#!/usr/bin/awk -f
#------------------------------------------------------------------------------#
#                            Programmed By Liz                                 #
#------------------------------------------------------------------------------#
# extract tracks & waypoints
#
# todo: indent
#===============================================================================
BEGIN {
   q="#"
   for(i=1;i<80;i++)
      q=q"-"
#------------------------------------------------------------------ erase tracks
   t=ENVIRON["HOME"]"/Map/garmin-trk.gpx"
   printf("<gpx>\n") > t
#--------------------------------------------------------------- erase waypoints
   w=ENVIRON["HOME"]"/Map/garmin-wpt.gpx"
   printf("<gpx>\n") > w
}
#===============================================================================
{
#----------------------------------------------------------------------- display
   switch($0)
   {
      case /<name>/ :
         d=$0
         sub(/<name>/,"",d)            # replacements
         sub(/<\/name>/,"",d)
         gsub(/&amp;/,"\\&",d)
         gsub(/&apos;/,"'",d)
         printf("%-80s\n",d)           # display
         system("sleep 0.02")
         break
   }
#----------------------------------------------------------------- file creation
   switch($0)
   {
#------------------------------------------------------------------------ tracks
      case /<trk>/ :                   # start trk
         f=1
         printf("%s\n",q) >> t
         printf("%s\n",$0) >> t
         break
      case /<\/trk>/ :
         f=0
      case /<.*trkpt>/ :
      case /<.*trkseg>/ :
         printf("%s\n",$0) >> t        # end trk
         break
#--------------------------------------------------------------------- waypoints
      case /<wpt.*>/ :                 # start wpt
         f=2
         printf("%s\n",q) >> w
         printf("%s\n",$0) >> w
         break
      case /[A-Za-z0-9;]$/ :           # fix wpt hanging line
         if(f==2)
            printf("%s, ",$0) >> w
         break
      case /<\/wpt>/ :                 # end wpt
         f=0
         printf("%s\n",$0) >> w
         break
#-------------------------------------------------------------------------- both
      case /<name>/ :
         if(f==1)
            printf("%s\n",$0) >> t
         if(f==2)
            printf("%s\n",$0) >> w
         break
      default :
#------------------------------------------------------------------------ tracks
         if(f==1)
            printf("%s",$0) >> t
#--------------------------------------------------------------------- waypoints
         if(f==2)
            printf("%s\n",$0) >> w
   }
}
#===============================================================================
END {
#------------------------------------------------------------------------ tracks
   printf("%s\n",q) >> t
   printf("</gpx>\n") >> t
#--------------------------------------------------------------------- waypoints
   printf("%s\n",q) >> w
   printf("</gpx>\n") >> w
}
#===============================================================================
# functions
#===============================================================================
